plugins {
    id 'com.jfrog.bintray' version '1.7.3'
    id "com.github.hierynomus.license" version "0.13.1"
    id 'com.github.ben-manes.versions' version '0.13.0'
}

repositories {
    jcenter()
}

configurations {
    testcode
}

dependencies {
    compile('xml-resolver:xml-resolver:1.2')
    compile('org.slf4j:slf4j-api:1.7.21')

    testcode sourceSets.test.output
}

tasks.bintrayUpload.dependsOn build

license {
    header rootProject.file('config/license_header.txt')

    // Toggle ignoreFailures to false to enable license-checking that will fail the build
    ignoreFailures true

    ext.name = 'HttpBuilder-NG Project'
    ext.year = Calendar.instance.get(Calendar.YEAR)

    excludes(['**/*.txt', '**/*.dtd', '**/*.xml', '**/*.ent', '**/*.jks'])
}

bintray {
    if (project.hasProperty('user') && project.hasProperty('key')) {
        user = project.property('user')
        key = project.property('key')

        publications = ['httpbPublication']

        pkg {
            repo = 'dclark'
            name = 'httpbuilder-ng-core'
            userOrg = 'http-builder-ng'
            desc = 'Groovy client for making http requests'
            licenses = ['Apache-2.0']
            labels = ['groovy']
            vcsUrl = 'https://github.com/http-builder-ng/http-builder-ng'
            websiteUrl = 'https://http-builder-ng.github.io/http-builder-ng/'
            issueTrackerUrl = 'https://github.com/http-builder-ng/http-builder-ng/issues'
            publicDownloadNumbers = true

            version {
                name = project.version
                vcsTag = "v${project.version}"
                gpg {
                    sign = true
                }
                mavenCentralSync {
                    sync = true
                    user = project.property('sonotypeUser')
                    password = project.property('sonotypePass')
                }
            }
        }
    }
}
